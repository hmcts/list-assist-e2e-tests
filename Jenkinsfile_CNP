#!groovy

properties([
  disableConcurrentBuilds(),
  parameters([
    string(
      name: 'FUNCTIONAL_TESTS_WORKERS',
      defaultValue: '4',
      description: 'Number of workers running functional tests'
    ),
    string(
      name: 'TAGS_TO_RUN',
      defaultValue: '',
      description: 'Optionally, run a single or multiple tags (comma separated e.g. @cui, @exui)'
    ),
    choice(
      name: 'browser',
      choices: ['chrome', 'firefox', 'webkit', 'edge'],
      description: 'Which browser to use'
    ),
    choice(
      name: 'TEST_SUITE',
      choices: ['bvt', 'regression', 'bvt_and_regression'],
      description: 'Select which test suite(s) to run',
      defaultValue: 'bvt'
    ),
    choice(
      name: 'ENVIRONMENT',
      choices: ['TRG', 'SIT'],
      description: 'Select which environment secrets to use',
      defaultValue: 'TRG'
    ),
  ])
])

@Library("Infrastructure")

def type = "nodejs"
def product = "snl-automation"
def component = "e2e-tests"
def channel = "#list-assist-qa-builds"

static Map<String, Object> secret(String secretName, String envVariable) {
  [
    $class: 'AzureKeyVaultSecret',
    secretType: 'Secret',
    name: secretName,
    envVariable: envVariable
  ]
}

def trgSecrets = [
  'snl-automation-bts-stg': [
    secret('test-password', 'TEST_PASSWORD'),
    secret('test-username', 'TEST_USERNAME'),
    secret('trg-base-url', 'TRG_BASE_URL'),
    secret('hmi-client-id', 'HMI_CLIENT_ID'),
    secret('hmi-client-secret', 'HMI_CLIENT_SECRET'),
    secret('hmi-scope', 'HMI_SCOPE'),
    secret('hmi-token-url', 'HMI_TOKEN_URL'),
    secret('hmi-token-tenant', 'HMI_TOKEN_TENANT'),
    secret('hmi-grant-type', 'HMI_GRANT_TYPE'),
    secret('hmi-trg-api-url', 'HMI_TRG_API_URL'),
  ]
]

def sitSecrets = [
  'snl-automation-bts-stg': [
    secret('test-password', 'TEST_PASSWORD'),
    secret('test-username', 'TEST_USERNAME'),
    secret('sit-base-url', 'SIT_BASE_URL'),
    secret('hmi-client-id', 'HMI_CLIENT_ID'),
    secret('hmi-client-secret', 'HMI_CLIENT_SECRET'),
    secret('hmi-scope', 'HMI_SCOPE'),
    secret('hmi-token-url', 'HMI_TOKEN_URL'),
    secret('hmi-token-tenant', 'HMI_TOKEN_TENANT'),
    secret('hmi-grant-type', 'HMI_GRANT_TYPE'),
    secret('hmi-sit-api-url', 'HMI_SIT_API_URL'),
  ]
]

def buildPlaywrightCommand(tags) {
  if (tags == null || tags.trim().isEmpty()) {
    return;
  }
  def tagList = tags.split(',').collect { it.trim() }
  def command = 'playwright test'
  tagList.each { tag ->
    if (!tag.isEmpty()) {
      command += " --grep ${tag} --project ${params.browser}"
    }
  }
  return command
}

withNightlyPipeline(type, product, component, 600) {
  // Select secrets based on ENVIRONMENT parameter
  def selectedSecrets = params.ENVIRONMENT == 'SIT' ? sitSecrets : trgSecrets
  loadVaultSecrets(selectedSecrets)
  env.CITIZEN_FRONTEND_BASE_URL = params.CITIZEN_FRONTEND_BASE_URL
  env.MANAGE_CASES_BASE_URL = params.MANAGE_CASES_BASE_URL
  env.FUNCTIONAL_TESTS_WORKERS = params.FUNCTIONAL_TESTS_WORKERS
  env.TEST_ENV = params.ENVIRONMENT
  enableSlackNotifications(channel)

  def yarnBuilder = new uk.gov.hmcts.contino.YarnBuilder(this)

  def browsers = ['chrome', 'firefox']
  def testConfigs = [
    [name: 'Add Participant Tests', grep: '@add-participant'],
    [name: 'Case Creation Tests', grep: '@add-new-case'],
    [name: 'Case Listing and Reporting Tests', grep: '@case-listing-and-reporting'],
    [name: 'Hearing Channel Tests', grep: '@hearing-channel'],
    [name: 'UI Tests', grep: '@ui-test'],
    [name: 'Add new user test', grep: '@add-user', skip: 'true'],
    [name: 'Daily cause reports test', grep: '@daily-cause-list-tests'],

    // Only run Amend Api Tests before listing in Chrome
    [name: 'Amend Api Tests before listing', grep: '@amend-api-test', skip: 'true', browsers: ['chrome']],
    // Only run Amend Api test after listing in Chrome
    [name: 'Amend Api test after listing', grep: '@amend-api-test-after-listing', skip: 'true', browsers: ['chrome']]
  ]

  def bvtStages = browsers.collectMany { browser ->
    testConfigs.findAll { !it.browsers || it.browsers.contains(browser) }.collect { cfg ->
      [
        name: "${browser.capitalize()} - ${cfg.name}",
        command: "playwright test --project=${browser} --grep ${cfg.grep}",
        skip: cfg.skip
      ]
    }
  } + [
    [name: 'Webkit - Smoke Tests', command: 'playwright test --project=webkit --grep @smoke']
  ]

  def regressionBrowsers = ['chrome']
  def regressionConfigs = [
    [name: 'Data Reporting Test', grep: '@data-reporting', path: 'regression']
  ]

  def regressionStages = regressionBrowsers.collectMany { browser ->
    regressionConfigs.collect { cfg ->
      [
        name: "${browser.capitalize()} - ${cfg.name}",
        command: "playwright test ${cfg.path} --project=${browser} --grep ${cfg.grep}"
      ]
    }
  }

  def runPlaywrightStage = { stageName, command, reportName, skipCreateCase = null ->
    stage(stageName) {
      try {
        if (skipCreateCase != null) {
          env.SKIP_CREATE_CASE = skipCreateCase
        }
        sh "TEST_ENV=${params.ENVIRONMENT} yarn ${command}"
      } catch (Error) {
        unstable(message: "${STAGE_NAME} is unstable: " + Error.toString())
      } finally {
        publishHTML([
          allowMissing: true,
          alwaysLinkToLastBuild: true,
          keepAll: true,
          reportDir: "playwright-report",
          reportFiles: 'index.html',
          reportName: reportName
        ])
        if (skipCreateCase != null) {
          env.SKIP_CREATE_CASE = null
        }
      }
    }
  }

  def runBvtStages = { stages ->
    stages.each { s ->
      runPlaywrightStage(s.name, s.command, s.name, s.skip ?: null)
    }
  }

  def runRegressionStages = { stages ->
    stages.each { s ->
      runPlaywrightStage(s.name, s.command, s.name)
    }
  }

  afterAlways('DependencyCheckNightly') {
  def regressionBlockChanged = false

  stage('Show Selected Environment') {
    echo "Selected ENVIRONMENT: ${params.ENVIRONMENT}"
  }

  stage('Detect Regression Folder Change For PR') {
  if (env.CHANGE_ID) {
    // Ensure the git remote is set correctly (useful for Jenkins environments)
    sh 'git remote set-url origin $(git config --get remote.origin.url)'
    // Make sure we have a full git history 
    sh 'git fetch --prune --unshallow || git fetch --all'
    // Fetch all remote branches to ensure we can diff against them
    sh 'git fetch origin +refs/heads/*:refs/remotes/origin/*'
    // Explicitly fetch the PR branch and the target branch for accurate comparison
    sh "git fetch origin ${env.CHANGE_BRANCH}:${env.CHANGE_BRANCH}"
    sh "git fetch origin ${env.CHANGE_TARGET}:${env.CHANGE_TARGET}"
    // Check if any files under any 'regression/' folder have changed between the PR branch and the target branch
    // If so, set regressionBlockChanged to true so the regression test pack will run
    def diffCmd = """git diff --name-only origin/${env.CHANGE_TARGET}...origin/${env.CHANGE_BRANCH} | grep -q 'regression/'"""
    regressionBlockChanged = (sh(script: diffCmd, returnStatus: true) == 0)
  }
}
    stage('Set up playwright') {
      try {
        yarnBuilder.yarn('setup')
      } catch (Error) {
        unstable(message: "${STAGE_NAME} is unstable: " + Error.toString())
      }
    }

    // Debugging environment variables
    stage('Debug Env Vars') {
      sh 'env | grep BASE_URL || true'
    }

    if (params.TAGS_TO_RUN && !params.TAGS_TO_RUN.isEmpty()) {
      stage("${params.TAGS_TO_RUN} E2E Tests ${params.browser}") {
        try {
          currentBuild.displayName = "${params.TAGS_TO_RUN} E2E Tests"
          yarnBuilder.yarn(buildPlaywrightCommand(params.TAGS_TO_RUN))
        } catch (Error) {
          unstable(message: "${STAGE_NAME} is unstable: " + Error.toString())
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "playwright-report",
            reportFiles: 'index.html',
            reportName: "${params.TAGS_TO_RUN} E2E Tests ${params.browser}"
          ])
        }
      }
    } else if (regressionBlockChanged) {
      runRegressionStages(regressionStages)
      runBvtStages(bvtStages)
    } else {
      if (params.TEST_SUITE == 'bvt') {
        runBvtStages(bvtStages)
      }
      if (params.TEST_SUITE == 'regression') {
        runRegressionStages(regressionStages)
      }
      if (params.TEST_SUITE == 'bvt_and_regression') {
        runBvtStages(bvtStages)
        runRegressionStages(regressionStages)
      }
    }
  }
}
